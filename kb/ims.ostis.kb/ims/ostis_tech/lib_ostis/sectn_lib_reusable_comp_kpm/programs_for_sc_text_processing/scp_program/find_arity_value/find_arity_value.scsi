proc_of_finding_arity_value
=> nrel_main_idtf:
	[scp-программа поиска значения арности отношения] (* <- lang_ru;; *);
=> nrel_main_idtf:
	[scp-program of finding relation's arity value] (* <-lang_en;; *);;

scp_program -> proc_of_finding_arity_value 
	(*	
	-> rrel_params: .proc_of_finding_arity_value_params 
		(*
		-> rrel_1: rrel_in: _relation;;
		-> rrel_2: rrel_out: _arity_value;;
		*);;

	-> rrel_operators: .proc_of_finding_arity_value_operator_set 
		(*
		-> rrel_init: .proc_of_finding_arity_value_operator1
			(*
			<- contAssign;;

			-> rrel_1: rrel_assign: rrel_scp_var: _arity_value;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: .proc_of_finding_arity_value_operator2;;
			*);;

		-> .proc_of_finding_arity_value_operator2
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: _node;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _relation;;

			-> rrel_set_1: rrel_assign: rrel_scp_var: _set_of_nodes;;

			=> nrel_then: .proc_of_finding_arity_value_operator3A;;
			=> nrel_else: .proc_of_finding_arity_value_operator_return;;
			*);;

		-> .proc_of_finding_arity_value_operator3A
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_nodes;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _node;;

			=> nrel_then: .proc_of_finding_arity_value_operator3B;;
			=> nrel_else: .proc_of_finding_arity_value_operator5;; 
			*);;

		-> .proc_of_finding_arity_value_operator3B
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: rrel_pos_const_perm: _arc1;;

			=> nrel_goto: .proc_of_finding_arity_value_operator3C;;
			*);;

		-> .proc_of_finding_arity_value_operator3C
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: arity;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: rrel_node: _node;;

			=> nrel_then: .proc_of_finding_arity_value_operator3D;;
			=> nrel_else: .proc_of_finding_arity_value_operator3A;;
			*);;

		-> .proc_of_finding_arity_value_operator3D
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_node: _node;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _arity;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_measurement;;

			=> nrel_then: .proc_of_finding_arity_value_operator4A;;
			=> nrel_else: .proc_of_finding_arity_value_operator3A;;
			*);;

		-> .proc_of_finding_arity_value_operator4A
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_const: rrel_node: _arity_of_relation;;

			=> nrel_goto: .proc_of_finding_arity_value_operator4B;;
			*);;

		-> .proc_of_finding_arity_value_operator4B
			(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_value;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .proc_of_finding_arity_value_operator4B_params
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _arity;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _arity_of_relation;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_of_finding_arity_value_operator4C;;
			*);;

		-> .proc_of_finding_arity_value_operator4C
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .proc_of_finding_arity_value_operator4D;;
			*);;

		-> .proc_of_finding_arity_value_operator4D
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _arity_of_relation;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_link: _value;;

			=> nrel_then: .proc_of_finding_arity_value_operator4E;;
			=> nrel_else: .proc_of_finding_arity_value_operator3A;;
			*);;

		-> .proc_of_finding_arity_value_operator4E
			(*
			<- contAssign;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _arity_value;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _value;;

			=> nrel_goto: .proc_of_finding_arity_value_operator4F;;
			*);;

		-> .proc_of_finding_arity_value_operator4F
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arity_of_relation;;

			=> nrel_goto: .proc_of_finding_arity_value_operator5;;
			*);;

		-> .proc_of_finding_arity_value_operator5
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_of_nodes;;

			=> nrel_goto: .proc_of_finding_arity_value_operator_return;;
			*);;

		-> .proc_of_finding_arity_value_operator_return
			(*
			<- return;;
			*);;
		*);;
	*);;
