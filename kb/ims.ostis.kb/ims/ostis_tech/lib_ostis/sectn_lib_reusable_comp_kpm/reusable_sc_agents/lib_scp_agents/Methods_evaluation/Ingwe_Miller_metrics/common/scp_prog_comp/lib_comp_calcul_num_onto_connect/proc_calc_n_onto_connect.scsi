scp_program -> proc_of_calculation_of_number_ontology_connections
	(*
  	-> rrel_params: .proc_of_calculation_of_number_ontology_connections_params 
		(*
		-> rrel_1: rrel_in: _set_of_vertexes;;
		-> rrel_2: rrel_in: _ontology;;
		-> rrel_3: rrel_in: _number_of_connections;;
    	*);;

	-> rrel_operators: .proc_of_calculation_of_number_ontology_connections_operator_set
    		(*
    		-> rrel_init: .proc_of_calculation_of_number_ontology_connections_operator_assign_zero 
	    		(*
			<- contAssign;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _number_of_connections;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_add_all_elem_to_temp_set;;
			*);;

    		-> .proc_of_calculation_of_number_ontology_connections_operator_add_all_elem_to_temp_set 
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_vertexes;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _all_vertexes;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_all_elements_loop_enter;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_all_elements_loop_enter
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _all_vertexes;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_loop_arc_erase;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_temp_set;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_loop_arc_erase
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_gen_set_all_arcs_of_vertex_temp;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_gen_set_all_arcs_of_vertex_temp
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _all_arcs_of_vertex_temp;; 

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_add_output_arcs_to_temp_set;;
			*);;			

		-> .proc_of_calculation_of_number_ontology_connections_operator_add_output_arcs_to_temp_set 
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _node;;
			-> rrel_2: rrel_assign: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _vertex;;

			-> rrel_set_2: rrel_fixed: rrel_scp_var: _all_arcs_of_vertex_temp;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _all_arcs_of_vertex_temp;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _concrete_arc;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_loop_arc_erase;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_erase_all_arcs_of_vertex_temp;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_loop_arc_erase
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_1;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_1
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_norole_relation: _relation;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _concrete_arc;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_2;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_3_checking;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_2_2
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: quasybinary_relation;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _relation;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_3_checking;;
			*);;	

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_3_checking
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _element1;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _concrete_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _element2;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_4;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_4
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _ontology;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _element1;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_5;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_5
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _ontology;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _element2;;

			=> nrel_then: .proc_of_calculation_of_number_ontology_connections_operator_increase;;
			=> nrel_else: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			*);;			

		-> .proc_of_calculation_of_number_ontology_connections_operator_increase 
			(*
	        	<- contAdd;;

	        	-> rrel_1: rrel_fixed: rrel_scp_var: _number_of_connections;;
	       	 	-> rrel_2: rrel_fixed: rrel_scp_var: _number_of_connections;;
			-> rrel_3: rrel_fixed: rrel_scp_const: [1];;
	         		
	        	=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_erase_not_ontology_arc_1_loop_enter;;
			*);;	

		-> .proc_of_calculation_of_number_ontology_connections_erase_all_arcs_of_vertex_temp
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _all_arcs_of_vertex_temp;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_all_elements_loop_enter;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_erase_temp_set
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _all_vertexes;;

			=> nrel_goto: .proc_of_calculation_of_number_ontology_connections_operator_return;;
			*);;

		-> .proc_of_calculation_of_number_ontology_connections_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;
