agent_of_calculation_normalized_number_of_vertexes_with_normal_degree
=> nrel_main_idtf:
	[агентная scp-программа подсчета нормированного количества вершин с нормальной степенью] (* <- lang_ru;; *);
	[agent scp-program of calculation normalized number of vertexes with normal degree] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_calculation_normalized_number_of_vertexes_with_normal_degree
	(*
	-> rrel_params: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_set 
		(*
		-> rrel_init: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1A
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1B;;
			*);;
                 
		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1B
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_to_calculate_normalized_number_of_vertexes_with_normal_degree;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _ontology;;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1D;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return;; // TODO add validation
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1D
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _ontology_classes;; 

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1E;;
			*);;		

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1E
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_subclasses_by_taxonomy_relation;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1E_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_const: ontology;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _ontology_classes;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr10;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1F;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1F
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr10;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1G;;
			*);;	

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1G
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_node: rrel_scp_var: _ontology_classes;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _class;;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1G1;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_delete_ontology_classes;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_delete_ontology_classes
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _ontology_classes;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_call_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1G1
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1H;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1H
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _class;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _ontology;;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C1;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1G;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C1
    			(*
			<- searchElStr5;;

			-> rrel_1: rrel_assign: rrel_scp_var: _subject_domain;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _ontology;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_ontology;;

        		=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_set_with_all_vertexes;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator7B;;
        		*);;

       	 	-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_set_with_all_vertexes
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _all_vertexes_of_ontology;; 

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C2;;
			*);;	

    		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C2
    			(*
			<- searchSetStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _subject_domain;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex_of_ontology;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_explored_relation;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _all_vertexes_of_ontology;;

        		=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C3;;
        		*);;

        	-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C3
    			(*
			<- searchSetStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _subject_domain;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex_of_ontology;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_not_maximum_studied_object_class;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _all_vertexes_of_ontology;;

        		=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C4;;
        		*);;

        	-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator1C4
    			(*
			<- searchSetStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _subject_domain;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex_of_ontology;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_maximum_studied_object_class;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _all_vertexes_of_ontology;;

        		=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_vertexes_cardinality;;
        		*);;

       	 	-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_vertexes_cardinality
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: rrel_link: _ontology_vertexes_cardinality;; 

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_vertexes_number;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_vertexes_number
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_cardinality_calculation;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_vertexes_number_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _all_vertexes_of_ontology;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _ontology_vertexes_cardinality;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_operator_waitreturn_vertexes_number;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_operator_waitreturn_vertexes_number
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_vertex_degrees_set;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_vertex_degrees_set
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _vertex_degrees;; 

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_fill_vertex_degrees_set;;
			*);;	

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_fill_vertex_degrees_set
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_all_ontology_vertex_degrees;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_fill_vertex_degrees_set_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _all_vertexes_of_ontology;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _ontology;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _vertex_degrees;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr2;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_filling_vertex_degrees_set;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_filling_vertex_degrees_set
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr2;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D1;;
			*);;		

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D1 
	    		(*
			<- contAssign;;

			-> rrel_1: rrel_assign: rrel_scp_var: _number_of_vertex_with_normal_degree;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D2_loop_enter;;
			*);;	

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D2_loop_enter
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _vertex_degrees;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _concrete_vertex_degree;;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D3_loop_arc_erase;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_E1_calc_result_value;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D3_loop_arc_erase
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_test_print;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_test_print
			(*
		  	<- printNl;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_link: _concrete_vertex_degree;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D4_is_normal_degree;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D4_is_normal_degree 
	    		(*
			<- ifGr;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _concrete_vertex_degree;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [9];;

			=> nrel_then: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D2_loop_enter;;
			=> nrel_else: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D5_increase;;  
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D5_increase 
			(*
	        	<- contAdd;;

	        	-> rrel_1: rrel_fixed: rrel_scp_var: _number_of_vertex_with_normal_degree;;
	        	-> rrel_2: rrel_fixed: rrel_scp_var: _number_of_vertex_with_normal_degree;;
			-> rrel_3: rrel_fixed: rrel_scp_const: [1];;
	         		
	        	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_D2_loop_enter;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_E1_calc_result_value 
			(*
	       	 	<- contDiv;;

	        	-> rrel_1: rrel_assign: rrel_scp_var: _result_value;;
	        	-> rrel_2: rrel_fixed: rrel_scp_var: _number_of_vertex_with_normal_degree;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _ontology_vertexes_cardinality;;
	         		
	        	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G1_erase_temp_set;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G1_erase_temp_set
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _all_vertexes_of_ontology;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G2_erase_temp_set;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G2_erase_temp_set
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _ontology_vertexes_cardinality;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G3_erase_temp_set;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_G3_erase_temp_set
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _vertex_degrees;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_delete;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_delete
			(*
		  	<- eraseEl;;

		  	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _ontology_classes;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_agent_work_result;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_agent_work_result
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_generation_of_metrics_answer;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_gen_agent_work_result_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: action_to_calculate_normalized_number_of_vertexes_with_normal_degree;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _result_value;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr3;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_generation_agent_work_result;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_generation_agent_work_result
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr3;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator7A;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator7A
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_successfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return;; 
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator7B
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_unsuccessfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return;; 
			*);;


/**************************************************************************Wrong Parametrs************************************************************/

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_call_proc_of_adding_validating_message 
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_call_proc_of_adding_validating_message_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [Wrong first parameter!It must be ontology];;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_waitreturn_proc_of_adding_validating_message
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return;;
			*);;

		-> .agent_of_calculation_normalized_number_of_vertexes_with_normal_degree_operator_return
			(*
			<- return;;
			*);;
		*);;
	*);;
